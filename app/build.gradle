import java.text.SimpleDateFormat

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
android {
    signingConfigs {
        appSigning {
            keyAlias 'AppSigning'
            keyPassword '123456'
            storeFile file('../signing/AppSigning.jks')
            storePassword '123456'
        }
    }
    compileSdkVersion 29
    buildToolsVersion "29.0.3"
    defaultConfig {
        applicationId "com.bugrui.jetpackproject"
        minSdkVersion 19
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        signingConfig signingConfigs.appSigning
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // 仅保留中文语种的资源
        resConfigs 'zh'

        // 仅保留 xxhdpi 图片资源（目前主流分辨率 1920 * 1080）
        resConfig 'xxhdpi'

        ndk {
            // armeabi：万金油架构平台（占用率：0%）
            // armeabi-v7a：曾经主流的架构平台（占用率：10%）
            // arm64-v8a：目前主流架构平台（占用率：90%）
            abiFilters 'armeabi-v7a', 'arm64-v8a'
        }
    }



    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildTypes {
        release {
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.appSigning
        }
    }

    packagingOptions {
        // 剔除这个包下的所有文件（除了签名信息）
        exclude 'META-INF/*******'
    }

    android.applicationVariants.all { variant ->

        variant.outputs.all {

            def outputPath = rootProject.ext.apkOutPath

            if (variant.name == "release")
                variant.packageApplicationProvider.get().outputDirectory = new File(outputPath + "release")

            def buildName = 'app_' + variant.name
            def type = "v" + variant.versionName
            def releaseApkName = buildName+ '_'+ type + '_' + systemTime() + ".apk"
            outputFileName = releaseApkName
        }


    }
}

/**
 * @return 获取当前系统时间
 */
def static systemTime() {
    def currentTime = System.currentTimeMillis()
    def date = new Date(currentTime)
    def time = new SimpleDateFormat("MM-dd-HH-mm").format(date)
    return time
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.0'
    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    //base
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta8'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.android.material:material:1.3.0-alpha01'
    //JetPack
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    //androidx kotlin dsl 方式编写代码布局
    implementation 'com.github.BugRui:kotlinCodeLayout:1.0.0-alpha'
    //事件总线
    implementation 'com.github.BugRui:LiveDataBus:1.2.2'
    //相机and相册
    implementation 'com.github.BugRui:CameraExtend:v1.1.0'
    //动态权限
    implementation 'com.github.BugRui:AndroidPermission:1.0.1'
    //多类型布局切换
    implementation 'com.github.BugRui:MultiTypeLayout:1.0.0'
    //toast
    implementation 'com.github.GrenderG:Toasty:1.4.2'
    //屏幕适配
    implementation 'me.jessyan:autosize:1.2.1'
    //智能下拉刷新框架
    implementation  'com.scwang.smart:refresh-layout-kernel:2.0.1'
    //侧滑删除
    implementation 'com.github.mcxtzhang:SwipeDelMenuLayout:V1.3.0'
    //多类型适配器
    implementation 'com.drakeet.multitype:multitype:4.2.0'
    //网络模块
    implementation 'com.github.BugRui:APIRequest:1.0.1'
    //微信SDK
    implementation 'com.tencent.mm.opensdk:wechat-sdk-android-with-mta:5.4.0'
    // X5内核
    implementation 'com.tencent.tbs.tbssdk:sdk:43903'
    //图片加载
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    kapt 'com.github.bumptech.glide:compiler:4.11.0'
    //压缩
    implementation 'top.zibin:Luban:1.1.8'
}